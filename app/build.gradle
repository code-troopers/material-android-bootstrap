apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'me.tatarka.retrolambda'
//Uncomment to add crashlytics
//apply plugin: 'io.fabric'

sdkManager {
    minSdkToolsVersion '24.4.1'
    emulatorVersion 'android-21'
    emulatorArchitecture 'armeabi-v7a'
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        applicationId "com.codetroopers.materialAndroidBootstrap"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testFunctionalTest true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        // Exclude file to avoid
        // Error: Duplicate files during packaging of APK
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'
        exclude 'META-INF/MANIFEST.MF'
    }

    //noinspection GroovyMissingReturnStatement
    lintOptions {
        disable 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def daggerVersion = "2.0.1"
    def autoparcelVersion = "0.3.1"
    def dexmakerVersion = "1.4"
    def supportTestVersion = "0.4.1"
    def espressoVersion = "2.2.1"
    def leakCanaryVersion = "1.3.1"
    def junitVersion = "4.12"
    def assertjVersion = "1.1.0"
    def mockitoVersion = "1.10.19"
    def icepickVersion = "3.1.0"
    def googleSupportVersion = '23.1.1'

    apt "com.google.dagger:dagger-compiler:" + daggerVersion
    apt 'com.github.frankiesardo:auto-parcel-processor:' + autoparcelVersion
    apt 'frankiesardo:icepick-processor:' + icepickVersion

    provided 'javax.annotation:jsr250-api:1.0'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:' + googleSupportVersion
    compile 'com.android.support:recyclerview-v7:' + googleSupportVersion
    compile 'com.android.support:design:' + googleSupportVersion
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.squareup.picasso:picasso:2.4.0'
    compile 'com.google.dagger:dagger:' + daggerVersion
    compile 'com.jakewharton.timber:timber:3.1.0'
    compile 'com.github.frankiesardo:auto-parcel:' + autoparcelVersion
    compile 'frankiesardo:icepick:' + icepickVersion

    //Uncomment to add crashlytics
    //compile('com.crashlytics.sdk.android:crashlytics:2.2.0@aar') {
    //  transitive = true;
    //}

    debugCompile 'com.squareup.leakcanary:leakcanary-android:' + leakCanaryVersion

    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:' + leakCanaryVersion

    testCompile 'junit:junit:' + junitVersion
    testCompile 'com.squareup.assertj:assertj-android:' + assertjVersion
    testCompile 'org.mockito:mockito-core:' + mockitoVersion

    androidTestApt "com.google.dagger:dagger-compiler:" + daggerVersion

    androidTestCompile 'junit:junit:' + junitVersion
    androidTestCompile 'com.squareup.assertj:assertj-android:' + assertjVersion
    androidTestCompile 'org.mockito:mockito-core:' + mockitoVersion
    androidTestCompile 'com.crittercism.dexmaker:dexmaker:' + dexmakerVersion
    androidTestCompile 'com.crittercism.dexmaker:dexmaker-dx:' + dexmakerVersion
    androidTestCompile 'com.crittercism.dexmaker:dexmaker-mockito:' + dexmakerVersion
    androidTestCompile 'com.android.support.test:runner:' + supportTestVersion
    androidTestCompile 'com.android.support.test:rules:' + supportTestVersion
    androidTestCompile 'com.android.support.test.espresso:espresso-core:' + espressoVersion
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:' + espressoVersion) {
        exclude module: 'appcompat'
        exclude module: 'support-v4'
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }
}

/**
 * "Warning:Conflict with dependency 'com.android.support:support-annotations'. Resolved versions for app (23.0.1) and test app (22.2.1) differ."
 *
 * ==> see http://stackoverflow.com/questions/31807182/android-espresso-issue-dependency-conflict
 * we have to force the version used to remove this warning.
 */
//noinspection GroovyAssignabilityCheck
configurations.all {
    resolutionStrategy {
        force 'com.android.support:support-annotations:23.1.0'
    }
}
